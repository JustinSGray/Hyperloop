
#All units in meters unless otherwise specified
###########################################################################################
# Design Variables
###########################################################################################
#inlet
despmtr r_fan 77.4
despmtr r_inlet .62

despmtr inlet_h1 .1

despmtr inlet_r2 .8
despmtr inlet_z2 .5
despmtr inlet_h2 .1

despmtr inlet_z3 1.5

#inlet scarfing
despmtr inlet_scarf_ang 0
despmtr inlet_scarf_offset 1

#pod & tube
despmtr r_pod 77.4
despmtr r_tube 4

despmtr utility_length 10 

despmtr capsule_width 1.35
despmtr capsule_height 1.1
despmtr n_seat_rows 14
despmtr length_per_seat_row 1.5

#nozzle
despmtr nozzle_r1 .65
despmtr nozzle_z1 .75

despmtr nozzle_r2 .232
despmtr nozzle_z2 2

despmtr nozzle_scarf_offset 1


#Air Bearings
despmtr bearing_height .1
despmtr bearing_sweep_angle 20
despmtr bearing_length 1.4
despmtr n_bearings 6
despmtr bearing_rotation 20 #angle rotation off vertical for each bearing pad




###########################################################################################
# Revolved Inlet
###########################################################################################
#inlet profile
#skbeg 0 r_pod inlet_z3 #end of top surface
#    linseg 0 r_fan inlet_z3 #start of bottom surface
#    spline 0 inlet_r2-inlet_h2 inlet_z2 
#    spline 0 r_inlet 0 #end of bottom surface
#    linseg 0 r_inlet+inlet_h1 0 
#    spline 0 inlet_r2+inlet_h2 inlet_z2
#   spline 0 r_pod inlet_z3
#skend
#revolve 0 0 0 0 0 1 360 
#fillet inlet_h1/2
###########################################################################################

###########################################################################################
#Scarfed Inlet
###########################################################################################
#nacelle
mark
 
set r r_inlet+inlet_h1
skbeg 0 r 0
    cirarc -r 0 0   0 -r 0
    cirarc  r 0 0   0  r 0
skend
set scarf_offset inlet_scarf_offset*(-r_pod+r)
set scarf_offset_1 inlet_scarf_offset*(-r_pod+r) #used later, so save it here
translate 0 scarf_offset 0

set r inlet_r2+inlet_h2
skbeg 0 r 0
    cirarc -r 0 0   0 -r 0
    cirarc  r 0 0   0  r 0
skend
set scarf_offset inlet_scarf_offset*(-r_pod+r)
translate 0 scarf_offset inlet_z2

set r r_pod
skbeg 0 r 0
    cirarc -r 0 0   0 -r 0
    cirarc  r 0 0   0  r 0
skend
translate 0 0 inlet_z3
loft 1

#inlet void 
mark
set r r_inlet #inlet entrace
skbeg 0 r 0
    cirarc -r 0 0   0 -r 0
    cirarc  r 0 0   0  r 0
skend
set scarf_offset inlet_scarf_offset*(-r_pod+r+inlet_h1)
translate 0 scarf_offset 0


set r inlet_r2-inlet_h2
skbeg 0 r 0
    cirarc -r 0 0   0 -r 0
    cirarc  r 0 0   0  r 0
skend
set scarf_offset inlet_scarf_offset*(-r_pod+r)+inlet_h2
translate 0 scarf_offset inlet_z2

set r r_fan #fan face
skbeg 0 r 0
    cirarc -r 0 0   0 -r 0
    cirarc  r 0 0   0  r 0
skend
translate 0 0 inlet_z3
loft 1

subtract #remove void from solid nacelle plug

#scarfing
set h 5*r_pod #cutting plane for scarf
box -h/2 -h/2 0 h h -2
rotatex inlet_scarf_ang -(r_inlet+inlet_h1)+scarf_offset_1 0 
subtract


###########################################################################################
#Utility Capsule for Batteries
###########################################################################################

#utilitiy container
set length utility_length
set width capsule_width
set height capsule_height
set z_pos inlet_z3+1
set utility_end z_pos+length
box -width/2 -height/2 0 width height length
translate 0 0 z_pos
#fillet .4

###########################################################################################
#Passenger Capsule
###########################################################################################
 
#passenger pressure vessel 
set length n_seat_rows*length_per_seat_row
set width capsule_width
set height capsule_height
set z_pos utility_end+1
set passenger_end z_pos+length
box -width/2 -height/2 0 width height length
translate 0 0 z_pos
#fillet .4

###########################################################################################
#Pod Outer Wall
###########################################################################################

#outer nacelle
set pod_length passenger_end
cylinder 0 0 0 0 0 pod_length r_pod
translate 0 0 inlet_z3


###########################################################################################
# Nozzle
###########################################################################################
#nozzle 
mark
skbeg 0 r_pod 0
    cirarc -r_pod 0 0  0 -r_pod 0
    cirarc r_pod 0 0  0 r_pod 0
skend

skbeg 0 nozzle_r1 nozzle_z1
    cirarc -nozzle_r1 0 nozzle_z1 0 -nozzle_r1 nozzle_z1
    cirarc nozzle_r1 0 nozzle_z1 0 nozzle_r1 nozzle_z1
skend
set scarf nozzle_scarf_offset*(-r_pod+nozzle_r1)
translate 0 scarf 0

skbeg 0 nozzle_r2 nozzle_z2
    cirarc -nozzle_r2 0 nozzle_z2 0 -nozzle_r2 nozzle_z2
    cirarc nozzle_r2 0 nozzle_z2 0 nozzle_r2 nozzle_z2
skend
set scarf nozzle_scarf_offset*(-r_pod+nozzle_r2)
translate 0 scarf 0

loft 1
translate 0 0 inlet_z3+passenger_end


###########################################################################################
# Air Bearings
###########################################################################################

#skid 1
set bearing_spacing (pod_length-(n_bearings*bearing_length))/(n_bearings-1)
patbeg i n_bearings

    set r_top r_pod
    set r_bottom 3*r_pod

    set alpha bearing_sweep_angle #arc size
    set theta (180-alpha)/2 #outside angle
    skbeg r_top*cosd(360-theta) r_top*sind(360-theta) 0 
        cirarc r_top*cosd(270) r_top*sind(270) 0 r_top*cosd(180+theta) r_top*sind(180+theta) 0
        linseg r_bottom*cosd(180+theta) r_bottom*sind(180+theta) 0
        cirarc r_bottom*cosd(270) r_bottom*sind(270) 0 r_bottom*cosd(360-theta) r_bottom*sind(360-theta) 0
        linseg r_top*cosd(360-theta) r_top*sind(360-theta) 0 
    skend
    extrude 0 0 bearing_length

    skbeg 0 r_tube 0
        cirarc -r_tube 0 0  0 -r_tube 0
        cirarc r_tube 0 0  0 r_tube 0
    skend
    translate 0 r_tube-r_top-bearing_height 0
    extrude 0 0 bearing_length

    intersect
    rotatez bearing_rotation 0 0 

    translate 0 0 inlet_z3+(i-1)*(bearing_length+bearing_spacing)
patend

#skid 2
patbeg i n_bearings

    set r_top r_pod
    set r_bottom 3*r_pod

    set alpha bearing_sweep_angle #arc size
    set theta (180-alpha)/2 #outside angle
    skbeg r_top*cosd(360-theta) r_top*sind(360-theta) 0 
        cirarc r_top*cosd(270) r_top*sind(270) 0 r_top*cosd(180+theta) r_top*sind(180+theta) 0
        linseg r_bottom*cosd(180+theta) r_bottom*sind(180+theta) 0
        cirarc r_bottom*cosd(270) r_bottom*sind(270) 0 r_bottom*cosd(360-theta) r_bottom*sind(360-theta) 0
        linseg r_top*cosd(360-theta) r_top*sind(360-theta) 0 
    skend
    extrude 0 0 bearing_length

    skbeg 0 r_tube 0
        cirarc -r_tube 0 0  0 -r_tube 0
        cirarc r_tube 0 0  0 r_tube 0
    skend
    translate 0 r_tube-r_top-bearing_height 0
    extrude 0 0 bearing_length

    intersect
    rotatez -bearing_rotation 0 0

    translate 0 0 inlet_z3+(i-1)*(bearing_length+bearing_spacing)
patend

###########################################################################################
# Tube
###########################################################################################
set r_tube_outer 1.02*r_tube
skbeg 0 r_tube 0
    cirarc -r_tube 0 0  0 -r_tube 0
    cirarc r_tube 0 0  0 r_tube 0
    cirarc -r_tube_outer 0 0  0 -r_tube_outer 0
    cirarc r_tube_outer 0 0  0 r_tube_outer 0
skend

set length pod_length+inlet_z3+nozzle_z2
translate 0 r_tube-r_top-bearing_height -.1*length


extrude 0 0 length*1.2

end 

